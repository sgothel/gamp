// Copyright (c) 2010-2025 Gothel Software e.K.
// Single Per Pixel Lighting (Example: GearsES2)

#if __VERSION__ >= 130
  #define attribute in
  #define varying out
#endif

uniform mat4 gcu_PMVMatrix[4]; // P, Mv, Mvi and Mvit
uniform vec3 gcu_Light0Pos;

attribute vec4 gca_Vertex;
attribute vec4 gca_Normal;

varying vec3 gcv_normal;
varying vec4 gcv_position;
varying vec3 gcv_cameraDir;

varying vec3 gcv_light0Dir;
varying float gcv_light0Attenuation;

const float constantAttenuation = 0.5; // 1.0;
const float linearAttenuation   = 0.001; // 0.0;
const float quadraticAttenuation= 0.0002; // 0.0;

void main(void)
{
    // Transforming The Vertex Position To ModelView-Space
    gcv_position  = gcu_PMVMatrix[1] * gca_Vertex; // vertex eye position

    // incl. projection
    gl_Position = gcu_PMVMatrix[0] * gcv_position;

    // Transforming The Normal To ModelView-Space
    gcv_normal = normalize((gcu_PMVMatrix[3] * gca_Normal).xyz);

    // Calculating The Vector From The Vertex Position To The Light Position
    gcv_light0Dir = gcu_Light0Pos - gcv_position.xyz;
    float d = length(gcv_light0Dir);
    gcv_light0Dir = normalize(gcv_light0Dir);
    gcv_light0Attenuation = 1.0 / (
                       constantAttenuation +
                       linearAttenuation    * d +
                       quadraticAttenuation * d * d );
    gcv_cameraDir  = normalize((gcu_PMVMatrix[2] * vec4(0,0,0,1.0)).xyz - gca_Vertex.xyz);
}
